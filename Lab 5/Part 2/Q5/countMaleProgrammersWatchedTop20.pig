movies = LOAD '/home/cloudera/Desktop/movies.csv' USING PigStorage(',') AS (movieId:int, title:chararray, genre:chararray);
ratings = LOAD '/home/cloudera/Desktop/rating.txt' USING PigStorage('\\t') AS (userId:chararray, movieId:int, rating:int, timestamp:int);
users = LOAD '/home/cloudera/Desktop/users.txt' USING PigStorage('|') AS (userId:chararray, age:int, gender:chararray,occupation:chararray,zipCode:int);
users = FILTER users BY occupation=='programmer' AND gender=='M';
rating_grouped = GROUP ratings BY movieId;
rating_calc = FOREACH rating_grouped GENERATE group AS movieId,1.0f*SUM(ratings.rating)/COUNT(ratings.rating) AS average:FLOAT;
genre_token = FOREACH movies GENERATE movieId, title, TOKENIZE(genre,'\\|') AS genreBag;
genre_bag = FOREACH genre_token GENERATE movieId, title, FLATTEN(genreBag) AS genre;
adventure_movies = FILTER genre_bag BY genre=='Adventure';
movie_rating_joined = JOIN adventure_movies BY movieId, rating_calc BY movieId;
ordered_movie_by_avg = ORDER movie_rating_joined BY average DESC;
top_20 = LIMIT ordered_movie_by_avg 20;
movie_rating = JOIN top_20 BY adventure_movies::movieId, ratings BY movieId;
movie_rating = FOREACH movie_rating GENERATE top_20::adventure_movies::movieId,ratings::userId;
male_programmer_movie_rating = JOIN movie_rating BY userId, users BY userId;
grouped = GROUP male_programmer_movie_rating ALL;
count = FOREACH grouped GENERATE 'Total Male Programmer Watchers: ', COUNT(male_programmer_movie_rating);
STORE count INTO 'Desktop/countMaleProgrammersWatchedTop20';
DUMP count;